#!/usr/bin/tcsh -f
set script_path=`realpath $0 | xargs dirname`
set script_name=`realpath $0 | xargs basename`
set test=smlp_toy_num_resp_mult
set log=${test}.log
set pref=Test83
\rm -f *.log ${pref}_* >& /dev/null
if($#argv > 0 ) then
    if( '-clean' == "$argv[1]" ) then
        exit(0)
    endif
endif
\ln -sf /usr/bin/python3.11 python3
if( 1 == `$script_path:h:h/src/run_smlp.py -h |& grep -c ^usage`) then
    echo "\nSMLP installation succeeded\n"
else
    echo "\nSMLP installation failed\n"
    exit(1)
endif
$script_path:h:h/src/run_smlp.py \
    -data ${test}.csv \
    -out_dir ./ \
    -pref $pref \
    -mode optimize -pareto t \
    -resp y1,y2 \
    -feat x,p1,p2 \
    -model dt_sklearn \
    -dt_sklearn_max_depth 15 \
    -spec ${test}_free_inps.json \
    -data_scaler min_max \
    -beta "y1>7 and y2>6" \
    -objv_names obj1,objv2,objv3 \
    -objv_exprs "(y1+y2)/2;y1/2-y2;y2" \
    -epsilon 0.05 \
    -delta_rel 0.01 \
    -save_model_config f \
    -mrmr_pred 0 \
    -plots f \
    -seed 10 \
    -log_time f \
    >& $log
\rm -f python3
set new_results=${pref}_smlp_toy_num_resp_mult_optimization_results.json 
set expected_results=${script_path}/smlp_dora_expected_results.json
if(-e $new_results) then
    set diff_cmd="diff $new_results $expected_results"
    echo $diff_cmd
    $diff_cmd
    if($status) then
        echo "\nSMLP passed, but results do not match expected. DORA test failed\n"
        exit 1
    else
        echo "\nDORA test PASSED\n"
        exit 0
    endif
else
    echo "\nERROR: Can't find file $new_results\nDORA test failed\n" 
    exit 1
endif
